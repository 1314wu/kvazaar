""" Build Kvazaa
"""

import os

Import('envs')
env = envs.env

sources = []
sources += env.Glob('*.c')
sources += env.Glob('extras/*.c')

sources += env.Glob('strategies/*.c')
sources += env.Glob('strategies/generic/*.c')
sources += envs.sse2.Object(env.Glob('strategies/sse2/*.c'))
sources += envs.sse41.Object(env.Glob('strategies/sse41/*.c'))
sources += envs.avx.Object(env.Glob('strategies/avx/*.c'))
sources += envs.avx2.Object(env.Glob('strategies/avx2/*.c'))
sources += envs.altivec.Object(env.Glob('strategies/altivec/*.c'))

sources += envs.asm.Object(env.Glob('strategies/x86_asm/*.c'))
if env['use_yasm']:
    sources += envs.asm.Object(env.Glob('strategies/x86_asm/*.asm'))
    sources += envs.asm.Object(env.Glob('extras/*.asm'))

prog = env.Program('kvazaar', sources)

env.Clean(prog, '.')  # Remove variant_dir with 'scons -c'.

Return('prog')
